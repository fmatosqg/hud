apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: "kotlin-allopen"
allOpen {
    annotation("com.fmatos.samples.hud.testability.Mockable")
}

apply from: 'secrets.gradle' // see README for instructions

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.fmatos.samples.hud"
        minSdkVersion 24
        targetSdkVersion 26
        versionCode 4
        versionName "1.0.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file("amazing-hud-key.keystore")
            storePassword secret.storePassword

            keyAlias "AmazingHud"
            keyPassword secret.keyPassword
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-deb"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }

    flavorDimensions "platform"

    productFlavors {
        phone {
            dimension "platform"
        }

        rpi3 {
            dimension "platform"
        }
    }
}

dependencies {

    def supportLibraryVersion = '26.0.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"

    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'joda-time:joda-time:2.9.9'

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.1'
    implementation 'io.reactivex:rxkotlin:2.0.0-RC1'

    def retrofitVersion = '2.2.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    def daggerVersion = '2.11'
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    implementation 'com.google.android.exoplayer:exoplayer:2.6.1'

    rpi3CompileOnly 'com.google.android.things:androidthings:1.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.8.47'
    testImplementation 'org.robolectric:robolectric:3.3.2'

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}


